// curvegpu:section fr_types begin
struct Fr {
  limbs: array<u32, 8>,
}

struct Fr16 {
  limbs: array<u32, 16>,
}
// curvegpu:section fr_types end

struct Params {
  count: u32,
  opcode: u32,
  _pad0: u32,
  _pad1: u32,
}

const FR_OP_COPY: u32 = 0u;
const FR_OP_ZERO: u32 = 1u;
const FR_OP_ONE: u32 = 2u;
const FR_OP_ADD: u32 = 3u;
const FR_OP_SUB: u32 = 4u;
const FR_OP_NEG: u32 = 5u;
const FR_OP_DOUBLE: u32 = 6u;
const FR_OP_NORMALIZE: u32 = 7u;
const FR_OP_EQUAL: u32 = 8u;
const FR_OP_MUL: u32 = 9u;
const FR_OP_SQUARE: u32 = 10u;
const FR_OP_TO_MONT: u32 = 11u;
const FR_OP_FROM_MONT: u32 = 12u;
// curvegpu:section fr_constants begin
const FR_LIMB16_MASK: u32 = 0xffffu;
const FR_QINV_NEG_16: u32 = 0xffffu;

const FR_MODULUS16: array<u32, 16> = array<u32, 16>(
  0x0001u, 0xf000u,
  0xf593u, 0x43e1u,
  0x7091u, 0x79b9u,
  0xe848u, 0x2833u,
  0x585du, 0x8181u,
  0x45b6u, 0xb850u,
  0xa029u, 0xe131u,
  0x4e72u, 0x3064u,
);
// curvegpu:section fr_constants end

@group(0) @binding(0) var<storage, read> input_a: array<u32>;
@group(0) @binding(1) var<storage, read> input_b: array<u32>;
@group(0) @binding(2) var<storage, read_write> output: array<u32>;
@group(0) @binding(3) var<uniform> params: Params;

// curvegpu:section fr_core begin
fn fr_zero() -> Fr {
  var z: Fr;
  z.limbs[0] = 0u;
  z.limbs[1] = 0u;
  z.limbs[2] = 0u;
  z.limbs[3] = 0u;
  z.limbs[4] = 0u;
  z.limbs[5] = 0u;
  z.limbs[6] = 0u;
  z.limbs[7] = 0u;
  return z;
}

fn fr_one() -> Fr {
  var z: Fr;
  z.limbs[0] = 0x4ffffffbu;
  z.limbs[1] = 0xac96341cu;
  z.limbs[2] = 0x9f60cd29u;
  z.limbs[3] = 0x36fc7695u;
  z.limbs[4] = 0x7879462eu;
  z.limbs[5] = 0x666ea36fu;
  z.limbs[6] = 0x9a07df2fu;
  z.limbs[7] = 0x0e0a77c1u;
  return z;
}

fn fr_one_regular() -> Fr {
  var z = fr_zero();
  z.limbs[0] = 1u;
  return z;
}

fn fr_rsquare_regular() -> Fr {
  var z: Fr;
  z.limbs[0] = 0xae216da7u;
  z.limbs[1] = 0x1bb8e645u;
  z.limbs[2] = 0xe35c59e3u;
  z.limbs[3] = 0x53fe3ab1u;
  z.limbs[4] = 0x53bb8085u;
  z.limbs[5] = 0x8c49833du;
  z.limbs[6] = 0x7f4e44a5u;
  z.limbs[7] = 0x0216d0b1u;
  return z;
}

fn fr_modulus() -> Fr {
  var z: Fr;
  z.limbs[0] = 0xf0000001u;
  z.limbs[1] = 0x43e1f593u;
  z.limbs[2] = 0x79b97091u;
  z.limbs[3] = 0x2833e848u;
  z.limbs[4] = 0x8181585du;
  z.limbs[5] = 0xb85045b6u;
  z.limbs[6] = 0xe131a029u;
  z.limbs[7] = 0x30644e72u;
  return z;
}

fn fr_predicate(value: bool) -> Fr {
  var z = fr_zero();
  if (value) {
    z = fr_one();
  }
  return z;
}

fn adc(a: u32, b: u32, carry: u32) -> vec2<u32> {
  let sum0 = a + b;
  let carry0 = select(0u, 1u, sum0 < a);
  let sum1 = sum0 + carry;
  let carry1 = select(0u, 1u, sum1 < sum0);
  return vec2<u32>(sum1, carry0 | carry1);
}

fn sbb(a: u32, b: u32, borrow: u32) -> vec2<u32> {
  let diff0 = a - b;
  let borrow0 = select(0u, 1u, a < b);
  let diff1 = diff0 - borrow;
  let borrow1 = select(0u, 1u, diff1 > diff0);
  return vec2<u32>(diff1, borrow0 | borrow1);
}

fn fr_is_zero(x: Fr) -> bool {
  return (x.limbs[0] | x.limbs[1] | x.limbs[2] | x.limbs[3] |
    x.limbs[4] | x.limbs[5] | x.limbs[6] | x.limbs[7]) == 0u;
}

fn fr_equal(x: Fr, y: Fr) -> bool {
  return (x.limbs[0] == y.limbs[0]) &&
    (x.limbs[1] == y.limbs[1]) &&
    (x.limbs[2] == y.limbs[2]) &&
    (x.limbs[3] == y.limbs[3]) &&
    (x.limbs[4] == y.limbs[4]) &&
    (x.limbs[5] == y.limbs[5]) &&
    (x.limbs[6] == y.limbs[6]) &&
    (x.limbs[7] == y.limbs[7]);
}

fn fr_gte(x: Fr, y: Fr) -> bool {
  if (x.limbs[7] != y.limbs[7]) {
    return x.limbs[7] > y.limbs[7];
  }
  if (x.limbs[6] != y.limbs[6]) {
    return x.limbs[6] > y.limbs[6];
  }
  if (x.limbs[5] != y.limbs[5]) {
    return x.limbs[5] > y.limbs[5];
  }
  if (x.limbs[4] != y.limbs[4]) {
    return x.limbs[4] > y.limbs[4];
  }
  if (x.limbs[3] != y.limbs[3]) {
    return x.limbs[3] > y.limbs[3];
  }
  if (x.limbs[2] != y.limbs[2]) {
    return x.limbs[2] > y.limbs[2];
  }
  if (x.limbs[1] != y.limbs[1]) {
    return x.limbs[1] > y.limbs[1];
  }
  return x.limbs[0] >= y.limbs[0];
}

fn fr_add_modulus(x: Fr) -> Fr {
  let q = fr_modulus();
  var z: Fr;
  var carry = 0u;
  var lane = adc(x.limbs[0], q.limbs[0], carry);
  z.limbs[0] = lane.x;
  carry = lane.y;
  lane = adc(x.limbs[1], q.limbs[1], carry);
  z.limbs[1] = lane.x;
  carry = lane.y;
  lane = adc(x.limbs[2], q.limbs[2], carry);
  z.limbs[2] = lane.x;
  carry = lane.y;
  lane = adc(x.limbs[3], q.limbs[3], carry);
  z.limbs[3] = lane.x;
  carry = lane.y;
  lane = adc(x.limbs[4], q.limbs[4], carry);
  z.limbs[4] = lane.x;
  carry = lane.y;
  lane = adc(x.limbs[5], q.limbs[5], carry);
  z.limbs[5] = lane.x;
  carry = lane.y;
  lane = adc(x.limbs[6], q.limbs[6], carry);
  z.limbs[6] = lane.x;
  carry = lane.y;
  lane = adc(x.limbs[7], q.limbs[7], carry);
  z.limbs[7] = lane.x;
  return z;
}

fn fr_sub_modulus(x: Fr) -> Fr {
  let q = fr_modulus();
  var z: Fr;
  var borrow = 0u;
  var lane = sbb(x.limbs[0], q.limbs[0], borrow);
  z.limbs[0] = lane.x;
  borrow = lane.y;
  lane = sbb(x.limbs[1], q.limbs[1], borrow);
  z.limbs[1] = lane.x;
  borrow = lane.y;
  lane = sbb(x.limbs[2], q.limbs[2], borrow);
  z.limbs[2] = lane.x;
  borrow = lane.y;
  lane = sbb(x.limbs[3], q.limbs[3], borrow);
  z.limbs[3] = lane.x;
  borrow = lane.y;
  lane = sbb(x.limbs[4], q.limbs[4], borrow);
  z.limbs[4] = lane.x;
  borrow = lane.y;
  lane = sbb(x.limbs[5], q.limbs[5], borrow);
  z.limbs[5] = lane.x;
  borrow = lane.y;
  lane = sbb(x.limbs[6], q.limbs[6], borrow);
  z.limbs[6] = lane.x;
  borrow = lane.y;
  lane = sbb(x.limbs[7], q.limbs[7], borrow);
  z.limbs[7] = lane.x;
  return z;
}

fn fr_add(x: Fr, y: Fr) -> Fr {
  var z: Fr;
  var carry = 0u;
  var lane = adc(x.limbs[0], y.limbs[0], carry);
  z.limbs[0] = lane.x;
  carry = lane.y;
  lane = adc(x.limbs[1], y.limbs[1], carry);
  z.limbs[1] = lane.x;
  carry = lane.y;
  lane = adc(x.limbs[2], y.limbs[2], carry);
  z.limbs[2] = lane.x;
  carry = lane.y;
  lane = adc(x.limbs[3], y.limbs[3], carry);
  z.limbs[3] = lane.x;
  carry = lane.y;
  lane = adc(x.limbs[4], y.limbs[4], carry);
  z.limbs[4] = lane.x;
  carry = lane.y;
  lane = adc(x.limbs[5], y.limbs[5], carry);
  z.limbs[5] = lane.x;
  carry = lane.y;
  lane = adc(x.limbs[6], y.limbs[6], carry);
  z.limbs[6] = lane.x;
  carry = lane.y;
  lane = adc(x.limbs[7], y.limbs[7], carry);
  z.limbs[7] = lane.x;
  if ((lane.y != 0u) || fr_gte(z, fr_modulus())) {
    return fr_sub_modulus(z);
  }
  return z;
}

fn fr_sub(x: Fr, y: Fr) -> Fr {
  var z: Fr;
  var borrow = 0u;
  var lane = sbb(x.limbs[0], y.limbs[0], borrow);
  z.limbs[0] = lane.x;
  borrow = lane.y;
  lane = sbb(x.limbs[1], y.limbs[1], borrow);
  z.limbs[1] = lane.x;
  borrow = lane.y;
  lane = sbb(x.limbs[2], y.limbs[2], borrow);
  z.limbs[2] = lane.x;
  borrow = lane.y;
  lane = sbb(x.limbs[3], y.limbs[3], borrow);
  z.limbs[3] = lane.x;
  borrow = lane.y;
  lane = sbb(x.limbs[4], y.limbs[4], borrow);
  z.limbs[4] = lane.x;
  borrow = lane.y;
  lane = sbb(x.limbs[5], y.limbs[5], borrow);
  z.limbs[5] = lane.x;
  borrow = lane.y;
  lane = sbb(x.limbs[6], y.limbs[6], borrow);
  z.limbs[6] = lane.x;
  borrow = lane.y;
  lane = sbb(x.limbs[7], y.limbs[7], borrow);
  z.limbs[7] = lane.x;
  if (lane.y != 0u) {
    return fr_add_modulus(z);
  }
  return z;
}

fn fr_neg(x: Fr) -> Fr {
  if (fr_is_zero(x)) {
    return fr_zero();
  }
  return fr_sub(fr_modulus(), x);
}

fn fr_double(x: Fr) -> Fr {
  return fr_add(x, x);
}

fn fr_normalize(x: Fr) -> Fr {
  if (fr_gte(x, fr_modulus())) {
    return fr_sub_modulus(x);
  }
  return x;
}

fn fr_unpack16(x: Fr) -> Fr16 {
  var z: Fr16;
  for (var i = 0u; i < 8u; i = i + 1u) {
    z.limbs[2u * i] = x.limbs[i] & FR_LIMB16_MASK;
    z.limbs[2u * i + 1u] = x.limbs[i] >> 16u;
  }
  return z;
}

fn fr_pack16(x: Fr16) -> Fr {
  var z: Fr;
  for (var i = 0u; i < 8u; i = i + 1u) {
    z.limbs[i] = x.limbs[2u * i] | (x.limbs[2u * i + 1u] << 16u);
  }
  return z;
}

fn fr16_gte_modulus(x: Fr16) -> bool {
  for (var i: i32 = 15; i >= 0; i = i - 1) {
    let idx = u32(i);
    let xLimb = x.limbs[idx];
    let qLimb = FR_MODULUS16[idx];
    if (xLimb != qLimb) {
      return xLimb > qLimb;
    }
  }
  return true;
}

fn fr16_sub_modulus(x: Fr16) -> Fr16 {
  var z: Fr16;
  var borrow = 0u;
  for (var i = 0u; i < 16u; i = i + 1u) {
    let lane = sbb(x.limbs[i], FR_MODULUS16[i], borrow);
    z.limbs[i] = lane.x & FR_LIMB16_MASK;
    borrow = lane.y;
  }
  return z;
}

fn fr_mul(x: Fr, y: Fr) -> Fr {
  let a = fr_unpack16(x);
  let b = fr_unpack16(y);
  var t: array<u32, 17>;

  for (var i = 0u; i < 16u; i = i + 1u) {
    var carry = 0u;
    let bi = b.limbs[i];
    for (var j = 0u; j < 16u; j = j + 1u) {
      let aLimb = a.limbs[j];
      let uv = t[j] + (aLimb * bi) + carry;
      t[j] = uv & FR_LIMB16_MASK;
      carry = uv >> 16u;
    }
    t[16] = carry;

    let m = (t[0] * FR_QINV_NEG_16) & FR_LIMB16_MASK;
    carry = 0u;
    for (var j = 0u; j < 16u; j = j + 1u) {
      let qLimb = FR_MODULUS16[j];
      let uv = t[j] + (m * qLimb) + carry;
      if (j > 0u) {
        t[j - 1u] = uv & FR_LIMB16_MASK;
      }
      carry = uv >> 16u;
    }
    let uv = t[16] + carry;
    t[15] = uv & FR_LIMB16_MASK;
    t[16] = uv >> 16u;
  }

  var z16: Fr16;
  for (var i = 0u; i < 16u; i = i + 1u) {
    z16.limbs[i] = t[i];
  }
  if ((t[16] != 0u) || fr16_gte_modulus(z16)) {
    z16 = fr16_sub_modulus(z16);
  }
  return fr_pack16(z16);
}
// curvegpu:section fr_core end

fn fr_dispatch(opcode: u32, a: Fr, b: Fr) -> Fr {
  if (opcode == FR_OP_COPY) {
    return a;
  }
  if (opcode == FR_OP_ZERO) {
    return fr_zero();
  }
  if (opcode == FR_OP_ONE) {
    return fr_one();
  }
  if (opcode == FR_OP_ADD) {
    return fr_add(a, b);
  }
  if (opcode == FR_OP_SUB) {
    return fr_sub(a, b);
  }
  if (opcode == FR_OP_NEG) {
    return fr_neg(a);
  }
  if (opcode == FR_OP_DOUBLE) {
    return fr_double(a);
  }
  if (opcode == FR_OP_NORMALIZE) {
    return fr_normalize(a);
  }
  if (opcode == FR_OP_EQUAL) {
    return fr_predicate(fr_equal(a, b));
  }
  if (opcode == FR_OP_MUL) {
    return fr_mul(a, b);
  }
  if (opcode == FR_OP_SQUARE) {
    return fr_mul(a, a);
  }
  if (opcode == FR_OP_TO_MONT) {
    return fr_mul(a, fr_rsquare_regular());
  }
  if (opcode == FR_OP_FROM_MONT) {
    return fr_mul(a, fr_one_regular());
  }
  return fr_zero();
}

fn fr_load_a(index: u32) -> Fr {
  let base = index * 8u;
  var z: Fr;
  z.limbs[0] = input_a[base + 0u];
  z.limbs[1] = input_a[base + 1u];
  z.limbs[2] = input_a[base + 2u];
  z.limbs[3] = input_a[base + 3u];
  z.limbs[4] = input_a[base + 4u];
  z.limbs[5] = input_a[base + 5u];
  z.limbs[6] = input_a[base + 6u];
  z.limbs[7] = input_a[base + 7u];
  return z;
}

fn fr_load_b(index: u32) -> Fr {
  let base = index * 8u;
  var z: Fr;
  z.limbs[0] = input_b[base + 0u];
  z.limbs[1] = input_b[base + 1u];
  z.limbs[2] = input_b[base + 2u];
  z.limbs[3] = input_b[base + 3u];
  z.limbs[4] = input_b[base + 4u];
  z.limbs[5] = input_b[base + 5u];
  z.limbs[6] = input_b[base + 6u];
  z.limbs[7] = input_b[base + 7u];
  return z;
}

fn fr_store(index: u32, value: Fr) {
  let base = index * 8u;
  output[base + 0u] = value.limbs[0];
  output[base + 1u] = value.limbs[1];
  output[base + 2u] = value.limbs[2];
  output[base + 3u] = value.limbs[3];
  output[base + 4u] = value.limbs[4];
  output[base + 5u] = value.limbs[5];
  output[base + 6u] = value.limbs[6];
  output[base + 7u] = value.limbs[7];
}

override WORKGROUP_SIZE: u32 = 64;

@compute @workgroup_size(WORKGROUP_SIZE)
fn fr_ops_main(@builtin(global_invocation_id) id: vec3<u32>) {
  let i = id.x;
  if (i >= params.count) {
    return;
  }
  fr_store(i, fr_dispatch(params.opcode, fr_load_a(i), fr_load_b(i)));
}
